(core:in-module core)


(set (quote list) (fn (:rest args) args))
(export (current-module) (quote list))

(set (quote def) (mac (symbol value)
  (list (quote set) 
        (list (quote quote) symbol)
         value)))
(export (current-module) (quote def))

(def defn (mac (name arg-list :rest body)
  (list (quote def) name
    (cons (quote fn)
      (cons
        (cons (quote :self) (cons name arg-list))
         (cons (quote progn) body))))))
(export (current-module) (quote defn))

(def defmac (mac (name arg-list :rest body)
  (list (quote def) name
    (cons (quote mac)
      (cons
        (cons (quote :self) (cons name arg-list))
         body)))))
(export (current-module) (quote defmac))

(defn single-quote-reader (stream char) (list (quote quote) (read stream)))
(set-macro-character #' single-quote-reader)

(defn comment-character (stream char)
  (if (= char #newline)
      (read stream)
      (comment-character stream (next stream))))

(set-macro-character #; comment-character)


;; we now have commentcharacters!
;; logic
(defmac when (cond :rest body)
  (list (quote if) cond 
    (cons (quote progn) body)
    (quote nil)))
(export (current-module) 'when)

(defmac unless (cond :rest body)
  (list (quote if) cond
    (quote nil)
    (cons progn body)))
(export (current-module) 'unless)
  
;; scope stuff
(defmac let (arg-list :rest body)
  (cons 
    (list (quote fn)
          ((fn (binding :self get-names)
            (when binding
              (cons (car (car binding)) (get-names (cdr binding)))))
            arg-list)
          (cons (quote progn) body))
    ((fn (binding :self get-vals)
        (when binding
          (cons (car (cdr (car binding))) (get-vals (cdr binding)))))
     arg-list)))
(export (current-module) 'let)

;; utility
(defn map (func seq)
  (unless (= nil seq)
    (cons (func (car seq))
    (map func (cdr seq)))))
(export (current-module) 'map)

(defn ++ (x) (+ x 1))
(export (current-module) '++)

;; import
(defn use-module (module1 module2)
  ((fn (syms :self dolist)
      (when syms
        (insert module1 (car syms))
        (dolist (cdr syms))))
   (get-symbols module2)))
(export (current-module) 'use-module)

(use-module &:hydra:io (current-module))
(in-module &:hydra:io)

(defn load (filename)
  (let ((fstream (open-file filename))
        ((module (current-module))))
   (while (not (endp fstream))
     (eval (read fstream)))
   (close-file fstream)
   (in-module module)))
(export (current-module) 'load)

(in-module &:hydra:core)
(def &:user (make-module "user"))
(use-module &:user &:hydra:core)
(use-module &:user &:hydra:io)
(use-module &:user &:hydra:foreign)
;;(use-module &:user &:hydra:concurrent)
(in-module &:user)